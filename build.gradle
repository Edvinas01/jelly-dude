buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        jcenter()
    }
    ext {
        gdxVersion = '1.9.5'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'

version = '0.1'

sourceCompatibility = 1.8

project.ext.mainClassName = 'com.edd.jelly.Launcher'
project.ext.assetsDir = new File('resources');

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }
    from files(project.assetsDir);

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

dist.dependsOn classes

dependencies {

    // Nicer fonts.
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

    // Core LigGDX dependencies.
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"

    // JBox2d + Liquid fun dependencies.
    compile files('libs/liquidfun/gdx-liquidfun-natives.jar')
    compile files('libs/liquidfun/gdx-liquidfun.jar')

    // Testing.
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.7'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}