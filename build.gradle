import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
    ext {
        kotlinVersion = '1.1.1'
        ashleyVersion = '1.7.3'
        groovyVersion = '2.4.7'
        detektVersion = '1.0.0.M9'
        guiceVersion = '4.1.0'
        spockVersion = '1.1-groovy-2.4-rc-3'
        gdxVersion = '1.9.6'

        // Raw asset directory.
        rawAssets = 'raw_assets/'

        // Raw tile-set directories with configs.
        // @formatter:off
        rawTiles = [[
                name : 'main_tileset',
                scale: 0.5
            ]
        ]
        // @formatter:on
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'http://dl.bintray.com/arturbosch/code-analysis' }
}

apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'idea'

version = '0.3.1-alpha'

sourceCompatibility = 1.8

project.ext.mainClassName = 'com.edd.jelly.Launcher'
project.ext.assetsDir = new File('resources')

// See:
// http://stackoverflow.com/questions/38131237/mixing-java-and-kotlin-in-gradle-project-kotlin-cannot-find-java-class
sourceSets {
    main.java.srcDirs = ['src/main/kotlin']
}

configurations {
    detekt
}

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
}

/**
 * Create distributable game .jar file.
 */
task dist(type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

/**
 * Run detect static code analysis.
 */
task detekt(type: JavaExec) {
    classpath = configurations.detekt
    main = 'io.gitlab.arturbosch.detekt.cli.Main'

    def params = [
            '-p', "$project.projectDir.absolutePath",
            '-c', "$project.projectDir/.detekt.yml",
            '-f', '.*test.*'
    ]

    args(params)
}

/**
 * Pack game tile-sets so that they could be used in Tiled.
 */
task packTiles << {
    def output = 'assets/textures/'

    // For each raw tile-set.
    (project.ext.rawTiles as List<Map<String, Object>>).each {
        def settings = new TexturePacker.Settings()
        def name = it.name as String

        // Default tile-set settings.
        settings.with {
            duplicatePadding = true
            grid = true

            maxWidth = 1024
            maxHeight = 1024

            paddingX = 2
            paddingY = 2
        }

        // Adjust scale if required.
        if (it.scale) {
            settings.scale = [it.scale] as float[]
        }

        def inputDir = "$project.ext.rawAssets$name"
        if (new File(inputDir).exists()) {
            TexturePacker.process(settings, inputDir, output, name)

            // Don't need the atlas file for tile-sets.
            new File("$output${name}.atlas").delete()
        }
    }
}

dist.dependsOn classes

idea {
    module {
        excludeDirs += file('logs')
    }
}

dependencies {

    // Kotlin!
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // Core LigGDX dependencies.
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"

    // ECS - Ashley.
    compile "com.badlogicgames.ashley:ashley:$ashleyVersion"

    // Dependency injection.
    compile "com.google.inject:guice:$guiceVersion"

    // Logging.
    compile 'org.apache.logging.log4j:log4j-core:2.8.1'
    compile 'org.apache.logging.log4j:log4j-api:2.8.1'

    // Other useful libraries.
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.0.pr1'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0.pr1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.0.pr1'
    compile 'com.xenomachina:kotlin-argparser:2.0.0'
    compile 'commons-io:commons-io:2.5'

    // JBox2d + Liquid fun dependencies.
    compile files('libs/jbox2d/jbox2d-library-2.3.1-SNAPSHOT.jar')
    compile files('libs/jbox2d/jbox2d-library-2.3.1-SNAPSHOT-sources.jar')

    // Testing.
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"

    // Static code analysis.
    detekt "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detektVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}